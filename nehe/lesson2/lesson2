sdl.window_t win;
sdl.gl.context_t ctx;
void init_all() {
    if (sdl.Init()) {
	print("init error: %s\n", sdl.GetError());
	exit(1);
    }
    sdl.gl.SetAttribute(sdl.gl.DOUBLE_BUFFER, 1);
    sdl.gl.SetAttribute(sdl.gl.DEPTH_SIZE, 24);
    win = sdl.CreateWindow("lesson2",
			   sdl.WINDOWPOS_CENTERED,
			   sdl.WINDOWPOS_CENTERED,
			   640, 480,
			   sdl.WINDOW_OPENGL |
			   sdl.WINDOW_RESIZABLE |
			   sdl.WINDOW_SHOWN);
    if (win == null) {
	print("create_window error: %s\n", sdl.GetError());
	exit(1);
    }
    ctx = sdl.gl.CreateContext(win);
    if (ctx == null) {
	print("create_context error: %s\n", sdl.GetError());
	exit(1);
    }
    sdl.gl.SetSwapInterval(1);
    sdl.gl.MakeCurrent(win, ctx);
}

void init() {
    /* Enable smooth shading */
    gl.ShadeModel(gl.SMOOTH);

    /* Set the background black */
    gl.ClearColor(0.0, 0.0, 0.0, 0.0);

    /* Depth buffer setup */
    gl.ClearDepth(1.0);

    /* Enables Depth Testing */
    gl.Enable(gl.DEPTH_TEST);

    /* The Type Of Depth Test To Do */
    gl.DepthFunc(gl.LEQUAL);

    /* Really Nice Perspective Calculations */
    gl.Hint(gl.PERSPECTIVE_CORRECTION_HINT, gl.NICEST);
}

void resize() {
    float64_t		ratio = win.w / win.h;

    /* Setup our viewport. */
    gl.Viewport(0, 0, win.w, win.h);

    /* change to the projection matrix and set our viewing volume. */
    gl.MatrixMode(gl.PROJECTION);
    gl.LoadIdentity();

    glu.Perspective(45.0, ratio, 0.1, 100.0);

    /* Make sure we're chaning the model view and not the projection */
    gl.MatrixMode(gl.MODELVIEW);

    /* Reset The View */
    gl.LoadIdentity();
}

void draw() {
    /* Clear The Screen And The Depth Buffer */
    gl.Clear(gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT);

    gl.LoadIdentity();

    /* Move Left 1.5 Units And Into The Screen 6.0 */
    gl.LoadIdentity();
    gl.Translate(-1.5, 0.0, -6.0);

    gl.Begin(gl.TRIANGLES);		/* Drawing Using Triangles */
    {
	gl.Vertex3( 0.0,  1.0, 0.0);	/* Top */
	gl.Vertex3(-1.0, -1.0, 0.0);	/* Bottom Left */
	gl.Vertex3( 1.0, -1.0, 0.0);	/* Bottom Right */
    }
    gl.End();				/* Finished Drawing The Triangle */

    /* Move Right 3 Units */
    gl.Translate(3.0, 0.0, 0.0);

    gl.Begin(gl.QUADS);			/* Draw A Quad */
    {
	gl.Vertex3(-1.0,  1.0, 0.0);	/* Top Left */
	gl.Vertex3( 1.0,  1.0, 0.0);	/* Top Right */
	gl.Vertex3( 1.0, -1.0, 0.0);	/* Bottom Right */
	gl.Vertex3(-1.0, -1.0, 0.0 );	/* Bottom Left */
    }
    gl.End();				/* Done Drawing The Quad */

    /* Draw it to the screen */
    sdl.gl.SwapWindow(win);
}

void loop() {
    uint8_t done = false;
    sdl.event_t	ev = new sdl.event_t;
    while (!done) {
	if (sdl.PollEvent(ev)) {
	    switch (ev.type) {
		case sdl.WINDOWEVENT:
		    switch (ev.event) {
			case sdl.WINDOWEVENT_RESIZED:
			    resize();
			    break;
			default:
			    break;
		    }
		    break;
		case sdl.KEYDOWN:
		    if (ev.keysym == sdl.K_ESCAPE)
			done = true;
			break;
		case sdl.QUIT:
		    done = true;
		    break;
	    }
	}
	draw();
    }
}
    
void finish_all() {
    sdl.gl.DeleteContext(ctx);
    sdl.DestroyWindow(win);
    sdl.Quit();
}

print("Esc: Exit\n");
init_all();
init();
resize();
loop();
finish_all();
